dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.7)dnl
AC_INIT(README)

if test "${enable_shared}" = "yes" ; then
    echo "Shared libraries not supported for cross compiling, ignored"
fi

AC_PROG_INSTALL

AC_CANONICAL_SYSTEM

# FIXME: We temporarily define our own version of AC_PROG_CC.  This is
# copied from autoconf 2.12, but does not call AC_PROG_CC_WORKS.  We
# are probably using a cross compiler, which will not be able to fully
# link an executable.  This should really be fixed in autoconf
# itself.

AC_DEFUN(CY_PROG_CC,
[AC_BEFORE([$0], [AC_PROG_CPP])dnl
AC_CHECK_PROG(CC, ${target_alias}-gcc, ${target_alias}-gcc)
test -z "$CC" && AC_MSG_ERROR([no acceptable cc found in \$PATH])
AC_PROG_CC_GNU

if test $ac_cv_prog_gcc = yes; then
  GCC=yes
dnl Check whether -g works, even if CFLAGS is set, in case the package
dnl plays around with CFLAGS (such as to build both debugging and
dnl normal versions of a library), tasteless as that idea is.
  ac_test_CFLAGS="${CFLAGS+set}"
  ac_save_CFLAGS="$CFLAGS"
  CFLAGS=
  AC_PROG_CC_G
  if test "$ac_test_CFLAGS" = set; then
    CFLAGS="$ac_save_CFLAGS"
  elif test $ac_cv_prog_cc_g = yes; then
    CFLAGS="-g -O2"
  else
    CFLAGS="-O2"
  fi
else
  GCC=
  test "${CFLAGS+set}" = set || CFLAGS="-g"
fi
])

CY_PROG_CC

extra_subdirs="";

case "${target}" in
  arm-*-elf*)
	archdir="arm";
        boards="armpid aeb-1 cma222";
	;;
  arm-*-coff*)
	archdir="arm";
        boards="armpid aeb-1 cma222";
	;;
  strongarm-*-elf*)
	archdir="arm";
        boards="sa1100dp sa-iop ebsa-285 sa1100mm";
	;;
  strongarm-*-coff*)
	archdir="arm";
        boards="sa1100dp sa-iop ebsa-285 sa1100mm";
	;;
  fr30-*-elf)
	archdir="fr30";
        boards="eval1";
	;;
  powerpc-*-eabi)
	archdir="ppc";
        boards="cma101 mbx evb403 yellowknife/yka yellowknife/ykb yellowknife/yklb";
        extra_subdirs="yellowknife";
	;;
  mn10300-*-*)
	archdir="am30";
        boards="eval1";
	;;
  mips*tx39*-elf)
	archdir="mips"
	boards="dve39 jmr3904"
	;;
  m68k-*-elf)
	archdir="m68k";
        boards="328ads";
	;;
  m68k-*-coff)
	archdir="m68k";
        boards="328ads";
	;;
  sparclite-*-elf)
	archdir="sparc";
        boards="mb8683x";
	;;
esac

if test -d ../../binutils ; then
    TMP_DIR=`cd ../../binutils; pwd`
fi

if test ! -z "${TMP_DIR}" -a -f ${TMP_DIR}/objcopy ; then
    OBJCOPY=${OBJCOPY:-${TMP_DIR}/objcopy};
else
    OBJCOPY=${OBJCOPY:-${target_alias}-objcopy}
fi

if test ! -z "${TMP_DIR}" -a -f ${TMP_DIR}/objdump ; then
    OBJDUMP=${OBJDUMP:-${TMP_DIR}/objdump};
else
    OBJDUMP=${OBJDUMP:-${target_alias}-objdump}
fi

if test ! -z "${TMP_DIR}" -a -f ${TMP_DIR}/ar ; then
    AR=${AR:-${TMP_DIR}/ar};
else
    AR=${AR:-${target_alias}-ar}
fi

if test ! -z "${TMP_DIR}" -a -f ${TMP_DIR}/ranlib ; then
    RANLIB=${RANLIB:-${TMP_DIR}/ranlib};
else
    RANLIB=${RANLIB:-${target_alias}-ranlib}
fi

OUTPUT="Makefile:Makefile.in"
MAKEFILES="Makefile"
MAKEFILEDEPS="Makefile.in common/common.mk"
if test ! -z "${archdir}" ; then
    MAKEFILEDEPS="${MAKEFILEDEPS} ${archdir}/cpu.mk"
fi
for x in $boards
do
	OUTPUT=${OUTPUT}" "${x}/Makefile
	OUTPUT=${OUTPUT}":"common/common_top.mk ;
	OUTPUT=${OUTPUT}":"${archdir}/${x}/board.mk ;
	OUTPUT=${OUTPUT}":"${archdir}/cpu.mk ;
	OUTPUT=${OUTPUT}":"common/common.mk ;
	MAKEFILES=${MAKEFILES}" "${x}/Makefile ;
	MAKEFILEDEPS=${MAKEFILEDEPS}" "${archdir}/${x}/board.mk ;
done

for x in $extra_subdirs
do
        OUTPUT=${x}/stamp:${archdir}/${x}/stamp.in" "${OUTPUT}
done

subdirs="$boards"
AC_SUBST(subdirs)
AC_SUBST(archdir)
AC_SUBST(MAKEFILES)
AC_SUBST(MAKEFILEDEPS)
AC_SUBST(srcdir)
AC_SUBST(CC)
AC_SUBST(AR)
AC_SUBST(OBJCOPY)
AC_SUBST(OBJDUMP)

AC_OUTPUT($OUTPUT)

