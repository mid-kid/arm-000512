dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.13)

AC_INIT(server-main.c)

AM_INIT_AUTOMAKE(multi-ice-server, 1.0)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

AC_PROG_CC
AC_CYGWIN
AC_EXEEXT
WINDRES=${WINDRES-windres}
AC_SUBST(WINDRES)

# In the cygwin32 environment, we need some additional flags.
AC_CACHE_CHECK([for cygwin32], multi_ice_cv_os_cygwin32,
[AC_EGREP_CPP(lose, [
#ifdef __CYGWIN32__
lose
#endif],[multi_ice_cv_os_cygwin32=yes],[multi_ice_cv_os_cygwin32=no])])
if test x$multi_ice_cv_os_cygwin32 = xyes; then
  WIN32LIBS="-lshell32 -ladvapi32 -lgdi32 -luser32"
  WIN32LDAPP="-Wl,--subsystem,console"
  WIN32RES="MICE-gdbserverres.o"
  MULTI_ICE_PLATFORM_SOURCE=server-win.c
else
  WIN32LIBS=
  WIN32LDAPP=
  WIN32RES=
  MULTI_ICE_PLATFORM_SOURCE=
fi
AC_SUBST(WIN32LIBS)
AC_SUBST(WIN32LDAPP)
AC_SUBST(WIN32RES)
AC_SUBST(MULTI_ICE_PLATFORM_SOURCE)

# This is the location of the ARM Multi-Ice header files, 
# and the toolconf library file.
AC_ARG_WITH(multi-ice,
[  --with-multi-ice=DIR    Build the multi-ICE dingus, using the
                           multi-ICE server found at DIR.],
[if test -f "${withval}/rdi.h"; then
  MULTI_ICE_INCLUDES="-I${withval}"
  MULTI_ICE_LIBS="-L${withval} -ltoolconf"
else
  AC_MSG_ERROR([--with-multi-ice: ${withval}/rdi.h does not exist.])
fi])

AC_SUBST(MULTI_ICE_INCLUDES)
AC_SUBST(MULTI_ICE_LIBS)

AM_CONDITIONAL(WINDOWS, test x$multi_ice_cv_os_cygwin32 = xyes)

dnl List of object files added by configure.

CONFIG_OBS=
CONFIG_DEPS=
CONFIG_SRCS=

dnl Checks for programs.

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h sys/file.h sys/ioctl.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(socket strtol)

AC_OUTPUT(Makefile)
